If Program.ArgumentCount = 1 Then 
  CSV_Path =  Program.GetArgument(1)
EndIf 
TextWindow.Title = ""
CSV_Interface()
Sub CSV_Interface
  CLLF = Text.GetCharacter(10)
  DQC  = Text.GetCharacter(34) 
  
  List_CSV_Integers = "CSV_INT"
  List_CSV_Length = "CSV_Length"
  CSV_Headers_Types = "0=TEXT;1=INTEGER;" 'Data Types
  
  TextWindow.WriteLine("Plugin : Import CSV File!")
  If LDFile.Exists( CSV_Path) = "False" Or CSV_Path = "" Then 
    CSV_Path = LDDialogs.OpenFile("csv","")
  EndIf 
  TextWindow.WriteLine("CSV Bytes : " + LDFile.Size(CSV_Path) + CLLF ) 
  
  Time_Start = Clock.ElapsedMilliseconds
  TextWindow.Write("Trimming   :")
  CSV_Name = LDText.Trim( LDText.Replace(LDFile.GetFile(CSV_Path),"Table","") )
  Time["Trimming"] = Clock.ElapsedMilliseconds - Time_Start
  TextWindow.WriteLine( Time["Trimming"])
  
  TextWindow.Write("Reading    :")
  Time_Read_Start = Clock.ElapsedMilliseconds 
  CSV_Data = LDFile.ReadCSV( CSV_Path )
  Time["Reading"] = Clock.ElapsedMilliseconds - Time_Read_Start
  TextWindow.WriteLine( Time["Reading"] ) 
  
  TextWindow.Write("Casting to List : ")
  Time_Cast_Start = Clock.ElapsedMilliseconds 
  List_CSV_Data = LDList.FromArray( CSV_Data )
  CSV_Data = ""
  Time["Casting"] = Clock.ElapsedMilliseconds - Time_Cast_Start
  TextWindow.WriteLine( Time["Casting"] ) 
  
  CSV_Header_Types = "0=Text;1=INTEGER;"
  CSV_Header_Data = LDList.GetAt(List_CSV_Data,1)
  
  TextWindow.Write("Pre Calculations:")
  
  Time_PreCalcs = Clock.ElapsedMilliseconds
  CSV_Count_Data = LDList.Count(List_CSV_Data )
  For I = 1 to CSV_Count_Data ' Array.GetItemCount(I_CSV_Array[1])
    LDList.Add( List_CSV_Integers , 1 ) 
    LDList.Add( List_CSV_Length , Array.GetItemCount( LDList.GetAt(List_CSV_Data,I) ) ) 
  EndFor
  CSV_Count_Length = LDList.Count(List_CSV_Length )
  Time["PCalc"] = Clock.ElapsedMilliseconds - Time_PreCalcs
  TextWindow.WriteLine( Time["PCalc"] )
  
  TextWindow.Write("Converting:")
  Time_Convert = Clock.ElapsedMilliseconds 
  ArrayToSQL()
  Time["Converting"] = Clock.ElapsedMilliseconds - Time_Convert
  TextWindow.WriteLine( Time["Converting"]) 
  
  TextWindow.Write("Creating Schema:")
  Time_Schema = Clock.ElapsedMilliseconds 
  CSVHeaders()
  Time["Schema"] = Clock.ElapsedMilliseconds - Time_Schema 
  TextWindow.WriteLine( Time["Schema"] ) 
  
  TextWindow.Write("Appending : " )
  Time_Append = Clock.ElapsedMilliseconds 
  CSV_SQL = "BEGIN;" + CLLF + Text.Append( CSV_HSQL , CSV_SQL ) + "COMMIT;"
  CSV_SQL = LDText.Replace( CSV_SQL ,"'NULL'","NULL")
  CSV_SQL = LDText.Replace( CSV_SQL ,"<<HEADERS>>", CSV_Headers_WO_Type)
  Time["Appending"] = Clock.ElapsedMilliseconds - Time_Append 
  TextWindow.WriteLine( Time["Appending"] ) 
  
  ElappsedTime = ( Clock.ElapsedMilliseconds - Time_Start )
  LDStatistics.SetArray( Time )
  Time["Misc"] = ElappsedTime - LDStatistics.Sum
  If Time["Misc"] <> Math.Abs(Time["Misc"]) Then
    Time["Misc"] = 0 
  EndIf 
  TextWindow.WriteLine("Misc Time : " + Time["Misc"] )
  
  TextWindow.WriteLine("Total Time : " + ElappsedTime + CLLF  ) 
  TextWindow.WriteLine("Bytes Per (ms) : " + (LDFile.Size(CSV_Path) / ElappsedTime) )  
  
  File.WriteContents(Program.Directory+"\CSV Import DUMP v2.sql", CSV_SQL ) 
  If LDDialogs.Confirm("Do you wish to open the converted SQL code","Confirm") = "Yes" Then 
    LDProcess.Start(Program.Directory+"\CSV Import DUMP v2.sql","")
  EndIf   
EndSub

Sub ArrayToSQL
  CSV_SQL = ""
  For I = 2 To CSV_Count_Length 
    CSV_TempArray_Data = LDList.GetAt( List_CSV_Data , I )
    CSV_Indicides_ofData = Array.GetAllIndices( CSV_TempArray_Data ) 
    CSV_SQL = CSV_SQL + "INSERT INTO " + DQC + CSV_Name + DQC +" <<HEADERS>>" + " VALUES('"
    If LDList.GetAt(List_CSV_Length,I) = LDList.GetAt( List_CSV_Length , 1) Then 
      For II = 1 To LDList.GetAt( List_CSV_Length , I ) 
        CSV_TempArray_II = LDText.Replace( CSV_TempArray_Data[ CSV_Indicides_ofData[II] ] ,"'","''")
        If CSV_TempArray_II = "" Or CSV_TempArray_II = " " Then 
          CSV_TempArray_II = "NULL"
        EndIf 
        CSV_SQL = CSV_SQL + CSV_TempArray_II
        If ( CSV_TempArray_II <> 0 And  Math.SquareRoot(CSV_TempArray_II) = 0 ) Or (CSV_TempArray_II = 0 ) Then 
          LDList.SetAt( List_CSV_Integers , II , 0 )
        EndIf 
        If II < LDList.GetAt( List_CSV_Length,I) Then 
          CSV_SQL= CSV_SQL + "','"
        EndIf 
      EndFor 
    Else 
      TextWindow.ForegroundColor = "RED"
      TextWindow.WriteLine("SKIPPED : " + I + "(" +LDList.GetAt(List_CSV_Length,I) + "/" +LDList.GetAt( List_CSV_Length , 1) +")")
      TextWindow.ForegroundColor = "WHITE"
    EndIf 
    CSV_SQL = CSV_SQL + "');" + CLLF
    TextWindow.Title = ("("+ I +"/" + CSV_Count_Length +") " + Math.Floor( (I/CSV_Count_Length) * 100 ) + "%" ) 
  EndFor
EndSub 

Sub CSVHeaders
  CSV_HSQL = "CREATE TABLE IF NOT EXISTS " + DQC + CSV_Name + DQC +" "
  CSV_HData = LDList.GetAt(List_CSV_Data,1)
  CSV_HIndex = Array.GetAllIndices( CSV_HData )
  CSV_HSQL = CSV_HSQL +  "("
  CSV_Headers_WO_Type = "("
  For I = 1 To LDList.GetAt( List_CSV_Length , 1) 
    CSV_HSQL = CSV_HSQL + DQC + CSV_HData[I] + DQC + " " + CSV_Header_Types [ LDList.GetAt( List_CSV_Integers , I ) ]
    CSV_Headers_WO_Type = CSV_Headers_WO_Type + DQC + CSV_HData[I] + DQC
    If I < LDList.GetAt( List_CSV_Length , 1) Then 
      CSV_HSQL = CSV_HSQL +","
      CSV_Headers_WO_Type = CSV_Headers_WO_Type+","
    EndIf 
  EndFor 
  CSV_HSQL = CSV_HSQL +");" + CLLF 
  CSV_Headers_WO_Type = CSV_Headers_WO_Type+")"
EndSub